# just is a command runner, Justfile is very similar to Makefile, but simpler.
# TODO update hostname here!
hostname := "MacBookPro"
elevate := "sudo"
# Variablen
flake_path := justfile_directory()
current_system := `uname -s`

# List all the just commands
default:
  @just --list

############################################################################
#
#  Darwin related commands
#
############################################################################

# Build and switch Darwin configuration, then switch home-manager
[group('darwin')]
rebuild-switch:
  darwin-rebuild switch --flake .

# Full system build and switch (nix build + darwin rebuild + home-manager)
[group('darwin')]
build-and-switch:
  nix build .#darwinConfigurations.{{hostname}}.system
  {{elevate}} darwin-rebuild switch --flake .

############################################################################
#
#  nix related commands
#
############################################################################

# Build the system configuration
[group('nix')]
build:
  nix build

# Update all the flake inputs
[group('nix')]
up:
  nix flake update

# Update specific input
# Usage: just upp nixpkgs
[group('nix')]
upp input:
  nix flake update {{input}}

# List all generations of the system profile
[group('nix')]
history:
  nix profile history --profile /nix/var/nix/profiles/system

# Open a nix shell with the flake
[group('nix')]
repl:
  nix repl -f flake:nixpkgs

# remove all generations older than 7 days
# on darwin, you may need to switch to root user to run this command
[group('nix')]
clean:
  {{elevate}} nix profile wipe-history --profile /nix/var/nix/profiles/system  --older-than 7d

# Garbage collect all unused nix store entries
[group('nix')]
gc:
  # garbage collect all unused nix store entries(system-wide)
  {{elevate}} nix-collect-garbage --delete-older-than 7d
  # garbage collect all unused nix store entries(for the user - home-manager)
  # https://github.com/NixOS/nix/issues/8508
  nix-collect-garbage --delete-older-than 7d

[group('nix')]
fmt:
  # format the nix files in this repo
  nix fmt {{flake_path}}

[group('nix')]
lint-and-fmt:
  #!/usr/bin/env bash
  echo "üîç Checking flake syntax..."
  nix flake check {{flake_path}} || exit 1
  echo "‚ú® Formatting code..."
  nix fmt {{flake_path}}
  echo "‚úÖ Lint and format completed!"

# Show all the auto gc roots in the nix store
[group('nix')]
gcroot:
  ls -al /nix/var/nix/gcroots/auto/

[group('nixos')]
build-devnix:
  {{elevate}} nixos-rebuild switch --flake .#devnix

[group('nixos')]
build-offnix:
  {{elevate}} nixos-rebuild switch --flake .#offnix
[group('nixos')]
build-playnix:
  {{elevate}} nixos-rebuild switch --flake .#playnix
